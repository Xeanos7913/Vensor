#version 460
#extension GL_ARB_separate_shader_objects : enable
#extension GL_EXT_buffer_reference : enable
#extension GL_EXT_buffer_reference_uvec2 : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_shader_atomic_float : enable
#extension GL_KHR_shader_subgroup_basic : enable
#extension GL_KHR_shader_subgroup_shuffle : enable
#extension GL_KHR_shader_subgroup_arithmetic : enable
#extension GL_KHR_shader_subgroup_shuffle_relative : enable

#define MAX_WG 256u
#define WARP_SIZE 32u
layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

layout(buffer_reference, std430, scalar) buffer tensor_data { float data[]; };
layout(buffer_reference, std430, scalar) buffer tensor_grad { float grad[]; };
layout(buffer_reference, std430, scalar) buffer tensor_strides { int strides[]; };
layout(buffer_reference, std430, scalar) buffer tensor_shape { int dims[]; };

struct TensorImpl {
    tensor_data data;
    tensor_grad grad;
    tensor_strides strides;
    tensor_shape shape;
    uint numel, ndim, requires_grad, is_leaf;
};

struct LayerNorm1DContext {
    TensorImpl input_tensor;    // [B, M, N]
    TensorImpl weight_tensor;   // [N]
    TensorImpl bias_tensor;     // [N]
    TensorImpl out_tensor;      // [B, M, N]
    TensorImpl save_mean;       // [B, M]
    TensorImpl save_rstd;       // [B, M]
    uint mode;                  // 0 = train, 1 = eval
    uint normalized_size;       // N
    uint batch_stride;          // M * N
    uint batch_size;
    uint accumulate_grad;
    float eps;
};

layout(buffer_reference, std430, scalar) buffer ContextBuffer { LayerNorm1DContext ctx; };

layout(push_constant) uniform PushConstants {
    ContextBuffer context;
    uvec3 grid_size;
    uint kernel_type;
} push;

shared float shmem[MAX_WG];
shared float shmem2[MAX_WG];

const uint VEC4 = 4u;
const uint VEC4S_PER_THREAD = 2u;
const uint FLOATS_PER_THREAD = VEC4 * VEC4S_PER_THREAD;
const uint NUM_WARPS = MAX_WG / WARP_SIZE;

vec4 loadVec4Contig(tensor_data buf, uint idx, uint remaining) {
    vec4 r = vec4(0.0);
    if (remaining >= 4u) {
        r.x = buf.data[idx + 0u];
        r.y = buf.data[idx + 1u];
        r.z = buf.data[idx + 2u];
        r.w = buf.data[idx + 3u];
    } else {
        if (remaining >= 1u) r.x = buf.data[idx + 0u];
        if (remaining >= 2u) r.y = buf.data[idx + 1u];
        if (remaining >= 3u) r.z = buf.data[idx + 2u];
    }
    return r;
}

vec4 loadVec4ContigGrad(tensor_grad buf, uint idx, uint remaining) {
    vec4 r = vec4(0.0);
    if (remaining >= 4u) {
        r.x = buf.grad[idx + 0u];
        r.y = buf.grad[idx + 1u];
        r.z = buf.grad[idx + 2u];
        r.w = buf.grad[idx + 3u];
    } else {
        if (remaining >= 1u) r.x = buf.grad[idx + 0u];
        if (remaining >= 2u) r.y = buf.grad[idx + 1u];
        if (remaining >= 3u) r.z = buf.grad[idx + 2u];
    }
    return r;
}

void storeVec4ContigGrad(tensor_grad buf, uint idx, vec4 v, uint remaining) {
    if (remaining >= 4u) {
        buf.grad[idx + 0u] = v.x;
        buf.grad[idx + 1u] = v.y;
        buf.grad[idx + 2u] = v.z;
        buf.grad[idx + 3u] = v.w;
    } else {
        if (remaining >= 1u) buf.grad[idx + 0u] = v.x;
        if (remaining >= 2u) buf.grad[idx + 1u] = v.y;
        if (remaining >= 3u) buf.grad[idx + 2u] = v.z;
    }
}

void atomicAddVec4(tensor_grad buf, uint idx, vec4 v, uint remaining) {
    if (remaining >= 1u) atomicAdd(buf.grad[idx + 0u], v.x);
    if (remaining >= 2u) atomicAdd(buf.grad[idx + 1u], v.y);
    if (remaining >= 3u) atomicAdd(buf.grad[idx + 2u], v.z);
    if (remaining >= 4u) atomicAdd(buf.grad[idx + 3u], v.w);
}

void blockReduceSum2(float val1, float val2, out float result1, out float result2) {
    uint tid = gl_LocalInvocationID.x;
    uint warp_id = tid / WARP_SIZE;
    // warp-level
    val1 = subgroupAdd(val1);
    val2 = subgroupAdd(val2);
    if (subgroupElect()) {
        shmem[warp_id] = val1;
        shmem2[warp_id] = val2;
    }
    barrier();
    // first warp finishes
    uint lane_id = tid % WARP_SIZE;
    if (warp_id == 0u) {
        float a = (lane_id < NUM_WARPS) ? shmem[lane_id] : 0.0;
        float b = (lane_id < NUM_WARPS) ? shmem2[lane_id] : 0.0;
        a = subgroupAdd(a);
        b = subgroupAdd(b);
        result1 = a;
        result2 = b;
    } else {
        result1 = 0.0;
        result2 = 0.0;
    }
}

void backward_kernel() {
    LayerNorm1DContext ctx = push.context.ctx;

    uint sample_idx = gl_WorkGroupID.x; // flattened [B, M]
    uint tid = gl_LocalInvocationID.x;
    uint lane = tid % WARP_SIZE;
    uint warp_id = tid / WARP_SIZE;

    uint N = ctx.normalized_size;
    uint base_idx = sample_idx * N;
    
    // Calculate batch size for scaling weight/bias gradients
    float inv_batch_size = 1.0 / ctx.batch_size;

    // tile params
    uint warp_tile_floats = WARP_SIZE * FLOATS_PER_THREAD;
    uint num_warps = NUM_WARPS;
    uint grid_stride = num_warps * warp_tile_floats;

    // Load saved statistics
    float mean_val = ctx.save_mean.data.data[sample_idx];
    float rstd_val = ctx.save_rstd.data.data[sample_idx];

    // First pass: compute sum(grad_out) and sum(grad_out * normalized_input)
    float t_sum_dout = 0.0;
    float t_sum_dout_norm = 0.0;

    for (uint tile_base = warp_id * warp_tile_floats; tile_base < N; tile_base += grid_stride) {
        uint thread_start = tile_base + lane * FLOATS_PER_THREAD;
        for (uint v = 0u; v < VEC4S_PER_THREAD; ++v) {
            uint idx = thread_start + v * VEC4;
            if (idx >= N) break;
            uint remaining = min(VEC4, N - idx);

            // Load gradient from output
            vec4 grad_out = loadVec4ContigGrad(ctx.out_tensor.grad, base_idx + idx, remaining);
            
            // Load input and compute normalized value
            vec4 input_val = loadVec4Contig(ctx.input_tensor.data, base_idx + idx, remaining);
            vec4 norm_val = (input_val - vec4(mean_val)) * vec4(rstd_val);
            
            // Load weight
            vec4 weight = loadVec4Contig(ctx.weight_tensor.data, idx, remaining);
            
            // grad_out is actually grad_out * weight for the normalized input gradient
            vec4 grad_norm = grad_out * weight;
            
            t_sum_dout += (grad_norm.x + grad_norm.y + grad_norm.z + grad_norm.w);
            t_sum_dout_norm += dot(grad_norm, norm_val);
        }
    }

    // Block reduction for the two sums
    float sum_dout = 0.0;
    float sum_dout_norm = 0.0;
    blockReduceSum2(t_sum_dout, t_sum_dout_norm, sum_dout, sum_dout_norm);
    barrier();

    // Store reduction results in shared memory
    if (tid == 0u) {
        shmem[0] = sum_dout;
        shmem[1] = sum_dout_norm;
    }
    barrier();

    sum_dout = shmem[0];
    sum_dout_norm = shmem[1];

    // Compute scaling factors for input gradient
    float scale1 = rstd_val / float(N);
    float scale2 = sum_dout / float(N);
    float scale3 = sum_dout_norm / float(N);

    // Second pass: compute gradients
    for (uint tile_base = warp_id * warp_tile_floats; tile_base < N; tile_base += grid_stride) {
        uint thread_start = tile_base + lane * FLOATS_PER_THREAD;
        for (uint v = 0u; v < VEC4S_PER_THREAD; ++v) {
            uint idx = thread_start + v * VEC4;
            if (idx >= N) break;
            uint remaining = min(VEC4, N - idx);

            // Load gradient from output
            vec4 grad_out = loadVec4ContigGrad(ctx.out_tensor.grad, base_idx + idx, remaining);
            
            // Load input and compute normalized value
            vec4 input_val = loadVec4Contig(ctx.input_tensor.data, base_idx + idx, remaining);
            vec4 norm_val = (input_val - vec4(mean_val)) * vec4(rstd_val);
            
            // Load weight
            vec4 weight = loadVec4Contig(ctx.weight_tensor.data, idx, remaining);
            
            // Compute weight gradient (grad_out * norm_val), scaled by 1/batch_size
            vec4 grad_weight = grad_out * norm_val * vec4(inv_batch_size);
            atomicAddVec4(ctx.weight_tensor.grad, idx, grad_weight, remaining);
            
            // Compute bias gradient (just grad_out), scaled by 1/batch_size
            vec4 grad_bias = grad_out * vec4(inv_batch_size);
            atomicAddVec4(ctx.bias_tensor.grad, idx, grad_bias, remaining);
            
            // Compute input gradient
            // grad_input = scale1 * (N * grad_norm - scale2 - norm_val * scale3)
            vec4 grad_norm = grad_out * weight;
            vec4 grad_input = vec4(scale1) * (float(N) * grad_norm - vec4(scale2) - norm_val * vec4(scale3));
            
            // Write or accumulate input gradient
            if (ctx.accumulate_grad == 1u) {
                atomicAddVec4(ctx.input_tensor.grad, base_idx + idx, grad_input, remaining);
            } else {
                storeVec4ContigGrad(ctx.input_tensor.grad, base_idx + idx, grad_input, remaining);
            }
            
            // Zero out output gradient to clean up compute tree
            storeVec4ContigGrad(ctx.out_tensor.grad, base_idx + idx, vec4(0.0), remaining);
        }
    }
}

void main() { backward_kernel(); }