#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_EXT_buffer_reference : enable
#extension GL_EXT_buffer_reference_uvec2 : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_KHR_shader_subgroup_basic : enable

// Warp-tiling parameters (matching CUDA kernel structure)
#define BM 128      // Block tile size M
#define BN 128      // Block tile size N
#define BK 8        // Block tile size K
#define WM 64       // Warp tile size M
#define WN 32       // Warp tile size N
#define WNITER 2    // Number of subwarp tiling steps in N dimension
#define TM 8        // Thread tile size M
#define TN 4        // Thread tile size N
#define NUM_THREADS 256

#define WARPSIZE 32

// Calculated constants
const uint WMITER = (WM * WN) / (WARPSIZE * TM * TN * WNITER);  // 2
const uint WSUBM = WM / WMITER;  // 32
const uint WSUBN = WN / WNITER;  // 16

layout(local_size_x = NUM_THREADS, local_size_y = 1, local_size_z = 1) in;

layout(buffer_reference, std430, scalar) buffer tensor_data {
    float data[];
};

layout(buffer_reference, std430, scalar) buffer tensor_grad {
    float grad[];
};

layout(buffer_reference, std430, scalar) buffer tensor_strides {
    int strides[];
};

layout(buffer_reference, std430, scalar) buffer tensor_shape {
    int dims[];
};

struct TensorImpl {
    tensor_data    data;
    tensor_grad    grad;
    tensor_strides strides;
    tensor_shape   shape;
    uint numel, ndim, requires_grad, is_leaf;
};

struct Conv2DContext {
    TensorImpl input_tensor;   // [B, C_in, H_in, W_in]
    TensorImpl weight_tensor;  // [C_out, C_in, KH, KW]
    TensorImpl bias_tensor;    // [C_out]
    TensorImpl out_tensor;     // [B, C_out, H_out, W_out]
    
    uint batch_size;
    uint in_channels;
    uint out_channels;
    uint in_height;
    uint in_width;
    uint out_height;
    uint out_width;
    uint kernel_h;
    uint kernel_w;
    uint stride_h;
    uint stride_w;
    uint padding_h;
    uint padding_w;
    uint dilation_h;
    uint dilation_w;
    
    uint use_bias;
    uint accumulate_grad;
    uint kernel_type;
    // GEMM dimensions
    // M = out_height * out_width (output spatial)
    // N = out_channels
    // K = in_channels * kernel_h * kernel_w
    
    uint m, n, k;
};

layout(buffer_reference, std430, buffer_reference_align = 128) buffer ContextBuffer { 
    Conv2DContext ctx; 
};

layout(push_constant) uniform PushConstants {
    ContextBuffer context;
    uvec3 grid_size;
    uint  kernel_type;
} push;

// Shared memory for block tiles (transposed layout for A)
shared float As[BM * BK];  // A tile (transposed: BK x BM)
shared float Bs[BK * BN];  // B tile (BK x BN)

// Helper function to get tensor offset
uint getTensorOffset(const TensorImpl t, uint idx0, uint idx1, uint idx2, uint idx3)
{
    if (t.ndim == 4u) {
        return idx0 * t.strides.strides[0] + 
               idx1 * t.strides.strides[1] + 
               idx2 * t.strides.strides[2] + 
               idx3 * t.strides.strides[3];
    } else if (t.ndim == 3u) {
        return idx0 * t.strides.strides[0] + 
               idx1 * t.strides.strides[1] + 
               idx2 * t.strides.strides[2];
    } else if (t.ndim == 2u) {
        return idx0 * t.strides.strides[0] + 
               idx1 * t.strides.strides[1];
    } else {
        return idx0 * t.strides.strides[0];
    }
}

// Im2col index computation - maps GEMM position to input tensor coordinates
// row: output spatial position (0 to M-1)
// col: filter position (0 to K-1)
// Returns input value at the corresponding position
float getIm2ColValue(uint batch, uint row, uint col)
{
    Conv2DContext c = push.context.ctx;
    
    // Decompose row into output spatial coordinates
    uint out_w = row % c.out_width;
    uint out_h = row / c.out_width;
    
    // Decompose col into filter coordinates
    uint kw = col % c.kernel_w;
    uint temp = col / c.kernel_w;
    uint kh = temp % c.kernel_h;
    uint in_c = temp / c.kernel_h;
    
    // Compute input spatial coordinates with stride, padding, dilation
    int in_h = int(out_h * c.stride_h) - int(c.padding_h) + int(kh * c.dilation_h);
    int in_w = int(out_w * c.stride_w) - int(c.padding_w) + int(kw * c.dilation_w);
    
    // Check bounds (padding)
    if (in_h < 0 || in_h >= int(c.in_height) || 
        in_w < 0 || in_w >= int(c.in_width)) {
        return 0.0;
    }
    
    // Access input tensor [batch, in_c, in_h, in_w]
    uint idx = getTensorOffset(c.input_tensor, batch, in_c, uint(in_h), uint(in_w));
    return c.input_tensor.data.data[idx];
}

void loadFromGmem(uint M, uint N, uint K, uint bkIdx, uint batch,
                  uint innerRowA, uint innerColA, uint innerRowB, uint innerColB)
{
    Conv2DContext c = push.context.ctx;
    const uint rowStrideA = (NUM_THREADS * 4) / BK;
    const uint rowStrideB = NUM_THREADS / (BN / 4);
    
    // Load A matrix (Im2Col virtual matrix) with vectorized loads and transpose to shared memory
    // A is [M x K], where M = out_h * out_w, K = in_c * kh * kw
    for (uint offset = 0; offset < BM; offset += rowStrideA) {
        uint globalRow = gl_WorkGroupID.y * BM + innerRowA + offset;
        uint globalCol = bkIdx + innerColA * 4;
        
        // Vectorized load (4 floats) via im2col indexing
        vec4 tmp = vec4(0.0);
        if (globalRow < M && globalCol < K) {
            tmp.x = getIm2ColValue(batch, globalRow, globalCol);
            if (globalCol + 1 < K) tmp.y = getIm2ColValue(batch, globalRow, globalCol + 1);
            if (globalCol + 2 < K) tmp.z = getIm2ColValue(batch, globalRow, globalCol + 2);
            if (globalCol + 3 < K) tmp.w = getIm2ColValue(batch, globalRow, globalCol + 3);
        }
        
        // Store transposed: As[col][row] = A[row][col]
        As[(innerColA * 4 + 0) * BM + innerRowA + offset] = tmp.x;
        As[(innerColA * 4 + 1) * BM + innerRowA + offset] = tmp.y;
        As[(innerColA * 4 + 2) * BM + innerRowA + offset] = tmp.z;
        As[(innerColA * 4 + 3) * BM + innerRowA + offset] = tmp.w;
    }
    
    // Load B matrix (Weight) - Weight is stored as [C_out, C_in, KH, KW]
    // We treat it as [N x K] where N = C_out, K = C_in * KH * KW
    // We want to load it as [BK x BN]
    for (uint offset = 0; offset < BK; offset += rowStrideB) {
        uint globalRow = bkIdx + innerRowB + offset;  // K dimension
        uint globalCol = gl_WorkGroupID.x * BN + innerColB * 4;  // N dimension (out_channels)
        
        // Vectorized load (4 floats)
        vec4 tmp = vec4(0.0);
        if (globalRow < K && globalCol < N) {
            // Decompose K index into (in_c, kh, kw)
            uint kw = globalRow % c.kernel_w;
            uint temp = globalRow / c.kernel_w;
            uint kh = temp % c.kernel_h;
            uint in_c = temp / c.kernel_h;
            
            // Weight[out_c, in_c, kh, kw]
            uint baseIdx = getTensorOffset(c.weight_tensor, globalCol, in_c, kh, kw);
            tmp.x = c.weight_tensor.data.data[baseIdx];
            
            if (globalCol + 1 < N) {
                uint idx1 = getTensorOffset(c.weight_tensor, globalCol + 1, in_c, kh, kw);
                tmp.y = c.weight_tensor.data.data[idx1];
            }
            if (globalCol + 2 < N) {
                uint idx2 = getTensorOffset(c.weight_tensor, globalCol + 2, in_c, kh, kw);
                tmp.z = c.weight_tensor.data.data[idx2];
            }
            if (globalCol + 3 < N) {
                uint idx3 = getTensorOffset(c.weight_tensor, globalCol + 3, in_c, kh, kw);
                tmp.w = c.weight_tensor.data.data[idx3];
            }
        }
        
        // Store in shared memory: Bs[k][n]
        uint smemIdx = (innerRowB + offset) * BN + innerColB * 4;
        Bs[smemIdx + 0] = tmp.x;
        Bs[smemIdx + 1] = tmp.y;
        Bs[smemIdx + 2] = tmp.z;
        Bs[smemIdx + 3] = tmp.w;
    }
}

void processFromSmem(inout float threadResults[WMITER * TM * WNITER * TN],
                     uint warpRow, uint warpCol,
                     uint threadRowInWarp, uint threadColInWarp)
{
    float regM[WMITER * TM];
    float regN[WNITER * TN];
    
    for (uint dotIdx = 0; dotIdx < BK; ++dotIdx) {
        // Populate registers for whole warptile
        for (uint wSubRowIdx = 0; wSubRowIdx < WMITER; ++wSubRowIdx) {
            for (uint i = 0; i < TM; ++i) {
                regM[wSubRowIdx * TM + i] = 
                    As[(dotIdx * BM) + warpRow * WM + wSubRowIdx * WSUBM + 
                       threadRowInWarp * TM + i];
            }
        }
        
        for (uint wSubColIdx = 0; wSubColIdx < WNITER; ++wSubColIdx) {
            for (uint i = 0; i < TN; ++i) {
                regN[wSubColIdx * TN + i] = 
                    Bs[(dotIdx * BN) + warpCol * WN + wSubColIdx * WSUBN + 
                       threadColInWarp * TN + i];
            }
        }
        
        // Execute warptile matmul
        for (uint wSubRowIdx = 0; wSubRowIdx < WMITER; ++wSubRowIdx) {
            for (uint wSubColIdx = 0; wSubColIdx < WNITER; ++wSubColIdx) {
                for (uint resIdxM = 0; resIdxM < TM; ++resIdxM) {
                    for (uint resIdxN = 0; resIdxN < TN; ++resIdxN) {
                        threadResults[(wSubRowIdx * TM + resIdxM) * (WNITER * TN) +
                                     (wSubColIdx * TN) + resIdxN] +=
                            regM[wSubRowIdx * TM + resIdxM] * 
                            regN[wSubColIdx * TN + resIdxN];
                    }
                }
            }
        }
    }
}

void forward(uint B, uint M, uint N, uint K)
{
    Conv2DContext c = push.context.ctx;
    
    const uint cRow = gl_WorkGroupID.y;
    const uint cCol = gl_WorkGroupID.x;
    const uint batch = gl_WorkGroupID.z;
    
    // Warp placement in threadblock tile
    const uint warpIdx = gl_LocalInvocationID.x / WARPSIZE;
    const uint warpCol = warpIdx % (BN / WN);
    const uint warpRow = warpIdx / (BN / WN);
    
    // Thread placement in warp subtile
    const uint threadIdxInWarp = gl_LocalInvocationID.x % WARPSIZE;
    const uint threadColInWarp = threadIdxInWarp % (WSUBN / TN);
    const uint threadRowInWarp = threadIdxInWarp / (WSUBN / TN);
    
    // Calculate indices for loading into shared memory
    const uint innerRowA = gl_LocalInvocationID.x / (BK / 4);
    const uint innerColA = gl_LocalInvocationID.x % (BK / 4);
    const uint innerRowB = gl_LocalInvocationID.x / (BN / 4);
    const uint innerColB = gl_LocalInvocationID.x % (BN / 4);
    
    // Allocate thread-local cache for results
    float threadResults[WMITER * TM * WNITER * TN];
    for (uint i = 0; i < WMITER * TM * WNITER * TN; ++i) {
        threadResults[i] = 0.0;
    }
    
    // Outer-most loop over block tiles
    for (uint bkIdx = 0; bkIdx < K; bkIdx += BK) {
        loadFromGmem(M, N, K, bkIdx, batch, innerRowA, innerColA, innerRowB, innerColB);
        barrier();
        
        processFromSmem(threadResults, warpRow, warpCol, threadRowInWarp, threadColInWarp);
        
        barrier();
    }
    
    // Write out the results
    for (uint wSubRowIdx = 0; wSubRowIdx < WMITER; ++wSubRowIdx) {
        for (uint wSubColIdx = 0; wSubColIdx < WNITER; ++wSubColIdx) {
            for (uint resIdxM = 0; resIdxM < TM; resIdxM += 1) {
                for (uint resIdxN = 0; resIdxN < TN; resIdxN += 1) {
                    uint globalRow = cRow * BM + warpRow * WM + wSubRowIdx * WSUBM + 
                                    threadRowInWarp * TM + resIdxM;
                    uint globalCol = cCol * BN + warpCol * WN + wSubColIdx * WSUBN + 
                                    threadColInWarp * TN + resIdxN;
                    
                    // Bounds check
                    if (globalRow < M && globalCol < N) {
                        uint i = (wSubRowIdx * TM + resIdxM) * (WNITER * TN) +
                                wSubColIdx * TN + resIdxN;
                        float result = threadResults[i];
                        
                        // Decompose globalRow back into spatial coordinates
                        uint out_w = globalRow % c.out_width;
                        uint out_h = globalRow / c.out_width;
                        uint out_c = globalCol;
                        
                        // Add bias if enabled
                        if (c.use_bias != 0u) {
                            uint biasIdx = getTensorOffset(c.bias_tensor, out_c, 0, 0, 0);
                            result += c.bias_tensor.data.data[biasIdx];
                        }

                        // Write output [batch, out_c, out_h, out_w]
                        uint idxOut = getTensorOffset(c.out_tensor, batch, out_c, out_h, out_w);
                        c.out_tensor.data.data[idxOut] = result;
                    }
                }
            }
        }
    }
}

void main()
{
    Conv2DContext c = push.context.ctx;
    uint B = max(c.batch_size, 1u);
    uint M = c.m;  // out_height * out_width
    uint N = c.n;  // out_channels
    uint K = c.k;  // in_channels * kernel_h * kernel_w
    
    //c.out_tensor.data.data[1] = K;
    forward(B, M, N, K);
}